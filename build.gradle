buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.12'

    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
    id 'net.researchgate.release' version '2.4.1'
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: "org.springframework.boot"
apply plugin: 'jacoco'
apply from: 'build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-docker.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-info.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/release-service.gradle'

description = 'EPAM Report portal. Rally Integration'

sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Edgware.RELEASE"
        mavenBom "org.springframework.boot:spring-boot-dependencies:1.5.9.RELEASE"
    }
}

project.ext {
    releaseMode = project.hasProperty("releaseMode")
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-bugtracking:4.0.3'
        compile 'com.epam.reportportal:commons:3.1.0'

    } else {
        compile('com.github.reportportal:commons-bugtracking:ed34aa9')
        compile 'com.github.reportportal:commons:3.1.0'
    }


    compile 'com.rallydev.rest:rally-rest-api:2.2.1'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'

}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

build.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
